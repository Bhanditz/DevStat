apply plugin: 'com.android.application'
apply plugin: 'android-apt'

// Load profile information from the standard maven settings file
def getMavenSettingsProfiles = {
    String userHome = System.getProperty("user.home");
    File mavenSettings = new File(userHome, ".m2/settings.xml")
    def output
    if (mavenSettings.canRead()) {
        def xmlSlurper = new XmlSlurper()
        output = xmlSlurper.parse(mavenSettings)
    } else {
        println "WARNING: Maven settings file cannot be found - builds cannot be signed"
        output = []
    }
    return output."profiles"."profile"
}

def getCredentials = {
    def profiles = getMavenSettingsProfiles()
    def creds = [location: "/not/found", password: "", keypass: "", alias: ""]
    for (profile in profiles) {
        if (profile."id".text() == "devstat") {
            def properties = profile."properties"
            creds = [location: properties."keystore.location".text(),
                     password: properties."keystore.password".text(),
                     keypass: properties."keystore.keypass".text(),
                     alias: properties."keystore.alias".text()]
        }
    }
    return creds
}

dependencies {
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'

    // Android Annotations
    apt "org.androidannotations:androidannotations:3.3.2"
    compile "org.androidannotations:androidannotations-api:3.3.2"
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processManifest.manifestOutputFile
        resourcePackageName android.defaultConfig.applicationId
    }
}

configurations {
    apt
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 22
        applicationId 'uk.co.ianfield.devstat'
        versionCode 5
        versionName '1.4'
    }
    signingConfigs {
        release {
            def credentials = getCredentials()
            storeFile file(credentials["location"])
            storePassword credentials["password"]
            keyPassword credentials["keypass"]
            keyAlias credentials["alias"]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        defaultFlavor {
            proguardFile 'proguard-rules.txt'
        }
    }

}