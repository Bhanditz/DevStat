apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.android.application'

// Load profile information from the standard maven settings file
def getMavenSettingsProfiles = {
    String userHome = System.getProperty("user.home");
    File mavenSettings = new File(userHome, ".m2/settings.xml")
    def output
    if (mavenSettings.canRead()) {
        def xmlSlurper = new XmlSlurper()
        output = xmlSlurper.parse(mavenSettings)
    } else {
        println "WARNING: Maven settings file cannot be found - builds cannot be signed"
        output = []
    }
    return output."profiles"."profile"
}

def getCredentials = {
    def profiles = getMavenSettingsProfiles()
    def creds = [location: "/not/found", password: "", keypass: "", alias: ""]
    for (profile in profiles) {
        if (profile."id".text() == "devstat") {
            def properties = profile."properties"
            creds = [location: properties."keystore.location".text(),
                     password: properties."keystore.password".text(),
                     keypass: properties."keystore.keypass".text(),
                     alias: properties."keystore.alias".text()]
        }
    }
    return creds
}

dependencies {
    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'

    compile project(":DevStatCore")

    // Android Annotations
    apt "org.androidannotations:androidannotations:3.2"
    compile "org.androidannotations:androidannotations-api:3.2"
}

apt {
    arguments {
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName android.defaultConfig.packageName
    }
}

configurations {
    apt
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.0'

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 21
        applicationId 'uk.co.ianfield.devstat'
        versionCode 3
        versionName '1.2'
    }
    signingConfigs {
        release {
            def credentials = getCredentials()
            storeFile file(credentials["location"])
            storePassword credentials["password"]
            keyPassword credentials["keypass"]
            keyAlias credentials["alias"]
        }
    }
    buildTypes {
        release {
            runProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        defaultFlavor {
            proguardFile 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java', 'GENERATED_FOLDER']
            resources.srcDirs = ['src/main/resources']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }
    }
}